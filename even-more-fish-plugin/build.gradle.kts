plugins {
    `java-library`
    `maven-publish`
    `jvm-test-suite`
    alias(libs.plugins.sonar)
    id("de.eldoria.plugin-yml.bukkit")
    id("org.evenmorefish.fish.shadow-conventions")
    id("org.evenmorefish.fish.publishing-conventions")
}

extra["plugin"] = true

group = "com.oheers.evenmorefish"
version = properties["project-version"] as String

description = "A fishing extension bringing an exciting new experience to fishing."

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
        vendor.set(JvmVendorSpec.ADOPTIUM)
    }
}


dependencies {
    api(project(":even-more-fish-api"))

    compileOnly(libs.paper.api) {
        version {
            strictly("1.21.1-R0.1-SNAPSHOT")
        }
    }

    compileOnly(libs.vault.api)
    compileOnly(libs.placeholder.api)

    compileOnly(libs.bundles.worldguard) {
        exclude("com.sk89q.worldedit", "worldedit-core")
        exclude("org.spigotmc", "spigot-api")
    }

    compileOnly(libs.bundles.worldedit)
    compileOnly(libs.bundles.redprotect) {
        exclude("net.ess3", "EssentialsX")
        exclude("org.spigotmc", "spigot-api")
        exclude("com.destroystokyo.paper", "paper-api")
        exclude("de.keyle", "mypet")
        exclude("com.sk89q.worldedit", "worldedit-core")
        exclude("com.sk89q.worldedit", "worldedit-bukkit")
        exclude("com.sk89q.worldguard", "worldguard-bukkit")
    }

    compileOnly(libs.aura.skills)

    compileOnly(libs.griefprevention)
    compileOnly(libs.mcmmo) {
        exclude("com.sk89q.worldguard", "worldguard-legacy")
    }
    compileOnly(libs.headdatabase.api)
    compileOnly(libs.playerpoints)

    implementation(libs.bstats)
    implementation(libs.inventorygui)
    implementation(libs.vanishchecker)
    api(libs.daisylib)

    implementation(libs.caffeine)
    implementation(libs.jdbi3.core)
    implementation(libs.jdbi3.sqlobject)

    implementation(libs.hikaricp)

    compileOnly(libs.bundles.flyway) {
        exclude("org.xerial", "sqlite-jdbc")
        exclude("com.mysql", "mysql-connector-j")
    }
    library(libs.friendlyid)
    library(libs.maven.artifact)
    compileOnly(libs.guava)

    library(libs.bundles.flyway) {
        exclude("org.xerial", "sqlite-jdbc")
        exclude("com.mysql", "mysql-connector-j")
    }

    implementation(libs.boostedyaml)

    library(libs.bundles.connectors)

    implementation(libs.dimensionfishing)

    compileOnly(libs.jspecify)

    // External Addons
    compileOnly(libs.nexo)
    compileOnly(libs.oraxen)
    compileOnly(libs.bundles.craftengine)
    compileOnly(libs.ecoitems)
    compileOnly("com.willfp:libreforge:4.81.0:all")
    compileOnly(libs.eco)
    compileOnly(libs.denizen.api)
    compileOnly(libs.itemsadder.api)
    compileOnly(libs.mmoitems.api)
    compileOnly(libs.mythic.lib)
}

bukkit {
    name = "EvenMoreFish"
    authors = listOf(
        "Oheers",
        "FireML",
        "sarhatabaot"
    )
    main = "com.oheers.fish.EvenMoreFish"
    version = project.version.toString()
    description = "A fishing extension bringing an exciting new experience to fishing."
    website = "https://github.com/EvenMoreFish/EvenMoreFish"
    apiVersion = "1.21"
    foliaSupported = true

    softDepend = listOf(
        "AuraSkills",
        "Denizen",
        "EcoItems",
        "GriefPrevention",
        "HeadDatabase",
        "ItemsAdder",
        "mcMMO",
        "Nexo",
        "Oraxen",
        "PlayerPoints",
        "PlaceholderAPI",
        "RedProtect",
        "Vault",
        "WorldGuard",
        // VanishChecker dependencies.
        "Essentials",
        "CMI",
        "SayanVanish",
        "AdvancedVanish"
    )
    loadBefore = listOf("AntiAC")
}


sonar {
    properties {
        property("sonar.projectKey", "EvenMoreFish_EvenMoreFish")
        property("sonar.organization", "evenmorefish")
        property("sonar.host.url", "https://sonarcloud.io")
    }
}

val copyAddons by tasks.registering(Copy::class) {
    // Make sure the plugin waits for the addons to be built first
    dependsOn(
        //":addons:addon-template:build"
    )

    //from(project(":addons:addon-template").layout.buildDirectory.dir("libs"))

    into(file("src/main/resources/addons"))
}

val copyVersions by tasks.registering(Copy::class) {
    dependsOn(
        ":versions:1-21:1-4:build",
        ":versions:1-21:5-11:build",
        ":versions:26-1:build",
        ":versions:26-2:build",
        ":versions:26-3:build"
    )

    from(project(":versions:1-21:1-4").layout.buildDirectory.dir("libs"))
    from(project(":versions:1-21:5-11").layout.buildDirectory.dir("libs"))
    from(project(":versions:26-1").layout.buildDirectory.dir("libs"))
    from(project(":versions:26-2").layout.buildDirectory.dir("libs"))
    from(project(":versions:26-3").layout.buildDirectory.dir("libs"))
    into(file("src/main/resources/versions"))
}


tasks {
    processResources {
        dependsOn(copyAddons)
        dependsOn(copyVersions)
    }

    clean {
        doFirst {
            val jitpack: Boolean = System.getenv("JITPACK").toBoolean()
            if (jitpack)
                return@doFirst

            for (file in File(project.projectDir, "src/main/resources/addons").listFiles()!!) {
                file.delete()
            }

            for (file in File(project.projectDir, "src/main/resources/versions").listFiles()!!) {
                file.delete()
            }
        }
    }

    compileJava {
        options.compilerArgs.add("-parameters")
        options.isFork = true
        options.encoding = "UTF-8"
    }

    shadowJar {
        dependsOn(":even-more-fish-api:shadowJar")
    }

    test {
        dependsOn(":even-more-fish-api:shadowJar")
    }

}

testing {
    suites {
        val test by getting(JvmTestSuite::class) {
            useJUnitJupiter()

            dependencies {
                implementation(project(":even-more-fish-api"))
                implementation(libs.junit.jupiter.api)
                implementation(libs.mockito.core)
                implementation(libs.boostedyaml)
                implementation(libs.paper.api) {
                    version {
                        strictly("1.21.1-R0.1-SNAPSHOT")
                    }
                }
                runtimeOnly(libs.junit.jupiter.engine)
            }

            targets {
                all {
                    testTask.configure {
                        useJUnitPlatform()
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        create<MavenPublication>("core") {
            groupId = project.group.toString()
            artifactId = project.name
            version = project.version.toString()

            from(components["shadow"])
        }
    }
}


